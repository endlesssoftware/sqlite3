!									!
! 2013 April 17								!
!									!
! The author disclaims copyright to this source code.  In place of	!
! a legal notice, here is a blessing:					!
!									!
!    May you do good and not evil.					!
!    May you find forgiveness for yourself and forgive others.		!
!    May you share freely, never taking more than you give.		!
!									!
!***********************************************************************!
!	This module builds a linker options file that contains the	!
! definition of the SQLite 3 symbol vector for either Alpha or Itanium.	!
!									!

@^A%MAKE_LINKER_VECTOR  V1.0%	! Identify myself...			!
13^T 10^T

0J Z"N <@FS%^ES% %;>		! Clean up the buffer, if it's there	!
	0J 0A-32"E D '		!   strip leading spaces		!
	ZJ -1A-32"E -D '	!   and trailing spaces			!
'				! endif buffer there			!
HXZ HK				! Save possible command			!

!		Set flags for our use, and close this file		!

[S ^XUS [S EDUS [S EHUS [S	! Save all the existing flags		!
ESUS [S EVUS [S ETUS [S EUUS [S	! so we can restore the user to		!
				! his original state on exit		!

^D				! Set Radix to 10			!
0^X				! Case-sensitive search			!
0ED				! ^ in search means ConTRL		!
				! Disable Y or _ if data loss		!
2EH				! Full error messages			!
0ES 0EV				! No typeout after search		!
2#16#128#512#8192ET
! Set ET flag as follows -						!
!     1 rw typeout in image mode					!
!     2 rw terminal is a scope						!
!     4 rw read lower case						!
!     8 rw ^T reads with no echo					!
!    16 rw cancel ^O on typeout						!
!    32 rw read with no wait, returns -1 if no input			!
!    64 rw detach flag							!
!   128 rw abort on error						!
!   256 rw truncate to set width					!
!   512 ro terminal is scope and watch is present			!
!  1024 ro terminal is refresh and watch is present			!
!  2048 xx not used							!
!  4096 rw 1 => 8bit, 0 => 7bit						!
!  8192 rw Surrogate escape on						!
! 16384 xx not used							!
! 32768 rw trap ^C and reset						!
-1EU				! No case flagging on output		!
!  EO  is Teco's Version Number						!
! 0EJ  is Job Number							!
! 1EJ  is KB Number							!
! 2EJ  is PPN								!
@EI%%				! Close this file			!

! --------------------------------------------------------------------- !
! *                     Load the date routine                           !
!                                                                       !
!       Executed from D$                                                !
!       Value returned in 0$                                            !
! --------------------------------------------------------------------- !
@^UD$^[
.U.P ZJ .U.Z
^B/32/16+1900\ @I% %
^B/32&15U.M
-1%.M"E @I%January% @O!GOTMON! '
-1%.M"E @I%February% @O!GOTMON! '
-1%.M"E @I%March% @O!GOTMON! '
-1%.M"E @I%April% @O!GOTMON! '
-1%.M"E @I%May% @O!GOTMON! '
-1%.M"E @I%June% @O!GOTMON! '
-1%.M"E @I%July% @O!GOTMON! '
-1%.M"E @I%August% @O!GOTMON! '
-1%.M"E @I%September% @O!GOTMON! '
-1%.M"E @I%October% @O!GOTMON! '
-1%.M"E @I%November% @O!GOTMON! '
@I%December%
!GOTMON! @I% %
^B&31+100\ 3R D ZJ 
Q.Z,.X0 Q.Z,.K
Q.PJ ^[$

! --------------------------------------------------------------------- !
! * Load symbol vector generation for Alpha and I64			!
! --------------------------------------------------------------------- !

GZ                                      ! Dump command line into buffer !
0J Z"N <@FS%^ES% %;>                    ! Clean up the buffer, if there !
    @^UX%% @^UY%% @^UZ%%                !   init filename buffers       !
    0J 0A-32"E D '                      !   strip leading spaces        !
    ZJ -1A-32"E -D '                    !   and trailing spaces         !
    0J
    < 0A:@^UY%% C 0A-^^="E C0;' >
    .,ZXX
'
 
@^A%...Loading existing symbol vector table...%
:QX"N
    HK                                  ! Clear text buffer             !
    :@ER%^EQX%"S                        ! If can open existing vector   !
        :EY                             !   then load it into buffer    !
    '                                   ! endif                         !
    0J HXT HK
    @^A%done...% 13^T 10^T
|
    @^A%unable to open % :GX @^A%...% 13^T 10^T
'

:QT"N
    @^A%...Generating Linker Options Symbol Vector...%

    @I%!% 13@I%% 10@I%%
    MD @I%! % G0 13@I%% 10@I%%
    @I%!
! The author disclaims copyright to this source code.  In place of
! a legal notice, here is a blessing:
!
!    May you do good and not evil.
!    May you find forgiveness for yourself and forgive others.
!    May you share freely, never taking more than you give.
!
!***********************************************************************
! This is automatically generated by MAKE_SYMBOL_VECTOR. DO NOT EDIT!!!
!
CASE_SENSITIVE=YES
%
    0U.P <					! Init pointer			!
        @I%SYMBOL_VECTOR = (%
        32 <
    	    @^U.L%% @^U.U%%			! Init symbol and alias		!
     	    <
	        Q.PQT:@^U.L%%
	        Q.PQT"V
	    	    Q.PQT-^^ :@^U.U%%
	        |
	    	    Q.PQT:@^U.U%%
	        '
	        %.PQT-^^,"E
		    @I%-% 13@I%% 10@I%%
		    3%.P^[
		    Q.PQT-^^S"E
			@I%    SPARE,- ! % G.L 13@I%% 10@I%%
			@I%    SPARE,%
		    | Q.PQT-^^D"E
			@I%    % G.L @I%=DATA,-% 13@I%% 10@I%%
			@I%    % G.U @I%/% G.L @I%=DATA,%
		    |
			@I%    % G.L @I%=PROCEDURE,-% 13@I%% 10@I%%
			@I%    % G.U @I%/% G.L @I%=PROCEDURE,%
		    ' '
	    	    < %.PQT-10"E 0;' >
	    	    %.P^[
	    	    0;
	        '
    	    >
        :QT-Q.P"E 0;' >
        -D @I%)% 13@I%% 10@I%%
    :QT-Q.P"E 0;' >

    @I%CASE_SENSITIVE=NO
%

    :@EW%^EQY/STM%"S                    ! If open vector file           !
        HP                              !   then dump out to file	!
        EF                              !   close vector file		!
        @^A%done...% 13^T 10^T
    |                                   ! else                          !
        @^A%Could not open vector file % :GY 13^T 10^T
    '                                   ! end if                        !
'

! --------------------------------------------------------------------- !
! *		    Now restore all the flags and exit			!
! --------------------------------------------------------------------- !

]S QSEU ]S QSET ]S QSEV ]S QSES ]S	! Restore all the flags we	!
QSEH ]S QSED ]S QS^X ]S			! saved at the start		!

^C

